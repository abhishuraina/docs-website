---
title: Get started with Roku agent
tags:
translate:
metaDescription: 'How to deploy roku agent monitoring in New Relic for your apps, including UI, copy/paste, and API options.'
redirects:
freshnessValidatedDate: never
---

## Overview

The New Relic Roku Agent is designed to track the behavior of Roku applications, focusing on system-level and video-related events. It uses the Event API to send data via a REST interface, supporting five types of events:

- **RokuSystem**: System events
- **VideoAction**: Video events
- **VideoErrorAction**: Error events
- **VideoAdAction**: Ad events
- **VideoCustomAction**: Custom events

## Prerequisites

To initialize the New Relic Roku Agent, you need:

- Your New Relic [account ID](/docs/accounts-partnerships/accounts/account-setup/account-id)
- Your New Relic <InlinePopover type="licenseKey"/>.

## Install and configure the Roku agent

<Steps>

<Step>
    Download the Roku Video Agent package and unzip it to access the following file structure:

   ```
   components/NewRelicAgent/
       NRAgent.brs
       NRAgent.xml
       NRTask.brs
       NRTask.xml
   source/
       NewRelicAgent.brs
   ```
  </Step>

  <Step>
      Copy the `NewRelicAgent` folder to the `components` directory and the `NewRelicAgent.brs` file to the `source` directory of your Roku app project.
  </Step>

  <Step>
        **Initialize the Agent**: Open your main BrightScript file (e.g., `Main.brs`). Initialize the New Relic Agent by calling the `NewRelic` function with your `ACCOUNT ID` and <InlinePopover type="licenseKey"/>.

        ```brightscript
        m.nr = NewRelic("ACCOUNT ID", "LICENSE KEY")
        ```
  </Step>

</Steps>

## Enable Event Capture

<Steps>

<Step>
    In your main BrightScript file (e.g., Main.brs), call the NewRelic function and store the returned object.
</Step>

<Step>
    Call `nrAppStarted` to log the application start event.
</Step>

<Step>
    (Optional) Use `NewRelicSystemStart` and `NewRelicVideoStart` to begin capturing system and video events.
</Step>

<Step>
    In the main wait loop, call `nrProcessMessage` to handle system events.
</Step>

</Steps>

### Example

Below is a basic example of integrating the New Relic Agent into a Roku application:

### Main.brs

```brightscript
    sub Main(aa as Object)
        screen = CreateObject("roSGScreen")
        m.port = CreateObject("roMessagePort")
        screen.setMessagePort(m.port)

        'Create the main scene that contains a video player
        scene = screen.CreateScene("VideoScene")
        screen.show()
            
        'Init New Relic Agent
        m.nr = NewRelic(“ACCOUNT ID“, “API KEY“)
            
        'Send APP_STARTED event
        nrAppStarted(m.nr, aa)
            
        'Pass NewRelicAgent object to the main scene
        scene.setField("nr", m.nr)
            
        'Activate system tracking
        m.syslog = NewRelicSystemStart(m.port)
        
        while (true)
            msg = wait(0, m.port)
            if nrProcessMessage(m.nr, msg) = false
                'It is not a system message captured by New Relic Agent
                if type(msg) = "roPosterScreenEvent"
                    if msg.isScreenClosed()
                        exit while
                    end if
                end if
            end if
        end while
    end sub
```

### VideoScene.xml

```xml
    <?xml version="1.0" encoding="utf-8" ?>
    <component name="VideoScene" extends="Scene"> 
	        <interface>
		            <!-- Field used to pass the NewRelicAgent object to the scene -->
		            <field id="nr" type="node" onChange="nrRefUpdated" />
	        </interface>
		
	        <children>
		            <Video
			                id="myVideo"
			                translation="[0,0]"
		            />
	        </children>
	
	        <!-- New Relic Agent Interface -->
	        <script type="text/brightscript" uri="pkg:/source/NewRelicAgent.brs"/>
	
	        <script type="text/brightscript" uri="pkg:/components/VideoScene.brs"/>
    </component>
```

### VideoScene.brs

```brightscript
sub init()
    m.top.setFocus(true)
    setupVideoPlayer()
end sub

function nrRefUpdated()
    m.nr = m.top.nr
    
    'Activate video tracking
    NewRelicVideoStart(m.nr, m.video)
end function

function setupVideoPlayer()
    videoUrl = "http://..."
    videoContent = createObject("RoSGNode", "ContentNode")
    videoContent.url = videoUrl
    videoContent.title = "Any Video"
    m.video = m.top.findNode("myVideo")
    m.video.content = videoContent
    m.video.control = "play"
end function
```


## Ad Tracking

The New Relic Roku Agent supports ad event monitoring for both the Roku Advertising Framework (RAF) and Google IMA.

### RAF Usage

1. **Pass the NRAgent Object**: Pass the `NRAgent` object (created with `NewRelic(accountId, apiKey)`) to the Ads Task using a field.

2. **Set Up the Ad Interface**: Inside the Ads Task, set up the ad interface and tracking callback.

```brightscript
   logFunc = Function(obj = Invalid as Dynamic, evtType = invalid as Dynamic, ctx = invalid as Dynamic)
       ' Call RAF tracker, passing the event and context
       nrTrackRAF(obj, evtType, ctx)
   End Function

   ' m.top.nr is the reference to the field where we have the NRAgent object
   adIface.setTrackingCallback(logFunc, m.top.nr)
   ```

3. **Example**: For a complete usage example, refer to the `VideoScene.brs` (function `setupVideoWithAds()`) and `AdsTask.brs` files in the repository.

### IMA Usage

1. **Create the IMA Tracker Object**: Initialize the IMA Tracker object.

   ```brightscript
   tracker = IMATracker(m.nr)
   ```

   - `m.nr` is the `NRAgent` object.

2. **Pass the Tracker Object**: Pass the tracker object to the IMA SDK Task using a field. Include the `IMATrackerInterface.brs` script in the task XML.

3. **Set Up Event Listeners**: Inside the task, set up event listeners for ad events.

   ```brightscript
   m.player.adBreakStarted = Function(adBreakInfo as Object)
       ' Ad break start code...
       
       ' Send AD_BREAK_START
       nrSendIMAAdBreakStart(m.top.tracker, adBreakInfo)
   End Function

   m.player.adBreakEnded = Function(adBreakInfo as Object)
       ' Ad break end code...
       
       ' Send AD_BREAK_END
       nrSendIMAAdBreakEnd(m.top.tracker, adBreakInfo)
   End Function

   ' Add event listeners for ad events
   m.streamManager.addEventListener(m.sdk.AdEvent.START, startCallback)
   m.streamManager.addEventListener(m.sdk.AdEvent.FIRST_QUARTILE, firstQuartileCallback)
   m.streamManager.addEventListener(m.sdk.AdEvent.MIDPOINT, midpointCallback)
   m.streamManager.addEventListener(m.sdk.AdEvent.THIRD_QUARTILE, thirdQuartileCallback)
   m.streamManager.addEventListener(m.sdk.AdEvent.COMPLETE, completeCallback)

   Function startCallback(ad as Object) as Void
       ' Send AD_START
       nrSendIMAAdStart(m.top.tracker, ad)
   End Function

   Function firstQuartileCallback(ad as Object) as Void
       ' Send AD_QUARTILE (first)
       nrSendIMAAdFirstQuartile(m.top.tracker, ad)
   End Function

   Function midpointCallback(ad as Object) as Void
       ' Send AD_QUARTILE (midpoint)
       nrSendIMAAdMidpoint(m.top.tracker, ad)
   End Function

   Function thirdQuartileCallback(ad as Object) as Void
       ' Send AD_QUARTILE (third)
       nrSendIMAAdThirdQuartile(m.top.tracker, ad)
   End Function

   Function completeCallback(ad as Object) as Void
       ' Send AD_END
       nrSendIMAAdEnd(m.top.tracker, ad)
   End Function
   ```

4. **Example**: For a complete usage example, refer to the `VideoScene.brs` (function `setupVideoWithIMA()`) and `imasdk.brs` files in the repository.